import org.yaml.snakeyaml.Yaml
/**
* This schema namespace is used to valide the params defined in config file(s) using a schema
* YAML file. This config script should be included and called through `schema.validate()` at the
* very bottom.
*/
schema {
    type_map = [
        'Integer'  : Integer,
        'String'   : [String, GString],
        'Number'   : Number,
        'List'     : List,
        'Bool'     : Boolean,
        'Namespace': Map,
        'Path'     : String
    ]
    required_properties = ['type']
    path_types = ['Path']
    custom_types = [:]


    /**
    * Load the schema file.
    */
    load_schema = { String file_path ->
        def schema_file = new File(file_path)
        Yaml parser = new Yaml()
        yaml = parser.load(schema_file.text)
        return yaml
    }

    /**
    * Check that given directory either exists and is writeable or is createable.
    */
    check_write_permission = { File dir ->
        if (dir.exists()) {
            if (!dir.canWrite()) {
                throw new Exception("   ### ERROR ###   The directory ${dir} is not writeable. Please verify and try again.")
            }
        } else {
            while (!dir.exists()) {
                dir = dir.getParentFile()
            }
            if (!dir.canWrite()) {
                throw new Exception("   ### ERROR ###   The directory ${dir} cannot be created. The closest existing parent directory ${dir.toString()} is not writable. Please verify permissions or change the input parameter.")
            }
        }
    }

    /**
    * Check type for paths (writeable or readable). Accepts path as String for validation.
    */
    check_path = { String p, String mode ->
        def File file = new File(p)
        if (mode == 'w') {
            schema.check_write_permission(file)
        } else {
            if ( ! (file.exists() && file.canRead()) ) {
                throw new Exception("${file} either doesn't exist or isn't readable.")
            }
        }
    }

    /**
    * Set default properties from the schema.
    */
    set_default_properties = { Map properties ->
        if (!properties.containsKey('help')) {
            properties['help'] = ''
        }
        if (!properties.containsKey('required')) {
            properties['required'] = false
        }
        if (properties['type'] in schema.path_types && !properties.containsKey('mode')) {
            properties['mode'] = 'r'
        }
        if (properties['type'] in schema.type_map['String'] && !properties.containsKey('allow_empty')) {
            properties['allow_empty'] = false
        }
    }

    /**
    * Check whether the required properties are defined for the corresponding parameter from the
    * schema yaml file.
    */
    check_required_properties = { String name, Map properties ->
        schema.required_properties.each { property ->
            if (!properties.containsKey(property)) {
                throw new Exception("Config file invalid. Parameter ${name} is missing property ${property}.")
            }
        }
    }

    /**
    * Check whether the required parameter is set from config file(s).
    */
    check_required = { Map options, String name, Map properties ->
        if (properties['required'] && !options.containsKey(name)) {
            throw new Exception("Config file invalid. Required parameter ${name} is missing.")
        }
    }

    /**
    * Check whether the parameter is set in the correct type, e.g. string, integer, etc.
    */
    check_type = { Map options, String name, String type ->
        if (! options.containsKey(name)) {
            return false
        }
        if (schema.type_map.containsKey(type)) {
            return schema.primitive_check_type(options, name, type)
        } else if (! schema.custom_types.containsKey(type)) {
            throw new Exception("Invalid parameter type ${type} found from schema.")
        }
        return true
    }

    /**
    * Check type of values
    */
    primitive_check_type = { Map options, String name, String type ->
        if (!(schema.type_map[type].any{ options[name] in it })) {
            throw new Exception("Invalid parameter type for parameter ${name}. Requires ${schema.type_map[type]} but received ${val.getClass()}.")
        }
        return true
    }

    /**
    * Check if string is empty if necessary
    */
    check_string_non_empty = { Map options, String name, Boolean allow_empty ->
        if (allow_empty) {
            return
        }
        if (options[name].isEmpty()) {
            throw new Exception("Parameter ${name} of type String is empty. Please enter a value.")
        }
    }

    /**
    * Check whether the parameter is set within the valid choices.
    */
    check_choices_singular = { Map options, String name, List choices ->
        if (!(options[name] in choices)) {
            throw new Exception("Invalid parameter ${name}. Valid values: ${choices}.")
        }
    }

    /**
    * Check whether list contains only valid choices
    */
    check_choices_list = { Map options, String name, List choices ->
        for (elem in options[name]) {
            if (!(elem in choices)) {
                throw new Exception("Invalid paramter ${name}. Valid values: ${choices}.")
            }
        }
    }

    /**
    * Check default for given property
    */
    check_default = { Map options, String name, Map properties ->
        if (properties.containsKey('default')) {
            options[name] = properties.default
        }
    }

    /**
    * For a given parameter, check whether the value is set properly from config file(s).
    */
    validate_parameter = { Map options, String name, Map properties ->
        // type is required
        schema.check_required_properties(name, properties)
        schema.set_default_properties(properties)
        if (schema.check_type(options, name, properties.type)) {
            if (properties.type == 'Namespace' && properties.containsKey('elements')) {
                properties.elements.each { key, val ->
                    schema.validate_parameter(options[name], key, val)
                }
            } else if (properties.type == 'List' && properties.containsKey('choices')) {
                schema.check_choices_list(options, name, properties.choices)
            } else if (properties.type == 'String') {
                schema.check_string_non_empty(options, name, properties.allow_empty)
            } else if (properties.type in schema.path_types) {
                schema.check_path(options[name], properties.mode)
            } else if (schema.custom_types.containsKey(properties.type)) {
                schema.custom_types[properties.type](options, name, properties)
            } else if (properties.containsKey('choices')) {
                schema.check_choices_singular(options, name, properties.choices)
            }
        } else {
            schema.check_default(options, name, properties)
            schema.check_required(options, name, properties)
        }
    }

    /**
    * Load custom types and validation methods.
    */
    load_custom_types = { String custom_types_path=null ->
        if (custom_types_path != null) {
            includeConfig "${custom_types_path}"
            if (! custom_schema_types.containsKey('types')) {
                throw new Exception("Failed to load custom types. Custom config must define namespace 'custom_schema_types' containing a Map 'types' defining the custom types as key and the corresponding function for validation as value.")
            }
            schema.custom_types = custom_schema_types.types
        }
    }

    /**
    * Main validation to call, to validate the params from config file(s) against the schema.
    */
    validate = { String file_path="${projectDir}/config/schema.yaml" ->
        params_schema = schema.load_schema(file_path)
        params_schema.each { key, val ->
            schema.validate_parameter(params, key, val)
        }
    }
}
