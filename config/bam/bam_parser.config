import java.util.zip.GZIPInputStream
/**
*   This bam_parser namespace is used to parse the header from a BAM file.
*   The information is loaded into a Map.
*/
bam_parser {
    /**
    *   Set up the decompressed SAM file reader for the input BAM
    */
    get_sam_reader = { String bam_path ->
        InputStream gzip_stream = new GZIPInputStream(new FileInputStream(bam_path))
        BufferedReader reader = new BufferedReader(new InputStreamReader(gzip_stream, 'UTF-8'))

        // Skip 8 characters to skip encoding info
        reader.skip(8)

        return reader
    }

    /**
    *   Parse read group line
    */
    parse_read_group = { String line ->
        def rg_map = [:]
        (line =~ /(?<tag>[A-Za-z][A-Za-z0-9]):(?<value>[ -~]+)/).each{ match, tag, value ->
            rg_map[tag] = value
        }

        return rg_map
    }

    /**
    *   Main parsing function for calling. Returns parsed data as a Map.
    */
    parse_bam_header = { String bam_path ->
        def sam_reader = bam_parser.get_sam_reader(bam_path)
        def line = sam_reader.readLine()
        def parsed_header = [:]
        parsed_header['read_group'] = []
        while (line && line.startsWith('@')) {
            // Handle different header lines here
            if (line.startsWith('@RG')) {
                parsed_header['read_group'].add(bam_parser.parse_read_group(line))
            }
            line = sam_reader.readLine()
        }

        return parsed_header
    }
}
